buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: "kotlin"
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
  mavenCentral()
}

dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile 'io.reactivex.rxjava2:rxjava:2.1.6'
        testCompile 'junit:junit:4.12'
        testCompile 'org.xerial:sqlite-jdbc:3.16.1'
        testCompile 'com.zaxxer:HikariCP:2.6.1'
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "org.nield"
archivesBaseName = "rxkotlin-jdbc"
version = "0.2.0"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'rxkotlin-jdbc'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Fluent RxJava JDBC extension functions for Kotlin'
                url 'https://github.com/thomasnield/rxkotlin-jdbc'

                scm {
                    connection 'scm:git://github.com/thomasnield/rxkotlin-jdbc.git'
                    developerConnection 'scm:git:git@github.com:thomasnield/rxkotlin-jdbc.git'
                    url 'https://github.com/thomasnield/rxkotlin-jdbc'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'thomasnield'
                        name 'Thomas Nield'
                        email 'thomasnield@live.com'
                    }
                }
            }
        }
    }
}
